
model {

  # Priors
  mean.p ~ dunif(0, 1)          # Detection intercept on prob. scale
  alpha0 <- logit(mean.p)       #   same on logit scale
  mean.psi ~ dunif(0, 1)        # Occupancy intercept on prob. scale
  beta0 <- logit(mean.psi)      #   same on logit scale
  for(k in 1:2){                # 2 terms in detection model
    alpha[k] ~ dnorm(0, 0.1)   # Covariates on logit(detection)
  }
  for(k in 1:11){               # 11 terms in occupancy model
    beta[k] ~ dnorm(0, 0.1)    # Covariates on logit(occupancy)
  }

  # Likelihood
  for (i in 1:M) {
    z[i] ~ dbern(psi[i])
    logit(psi[i]) <- beta0 + inprod(beta[], occDM[i,])  # slick !
    for (j in 1:J) {
      y[i,j] ~ dbern(z[i] * p[i,j]) # In WinBUGS need 'straw man'
      logit(p[i,j]) <- alpha0 +     # detection (p) intercept
           alpha[1] * elev[i] +       # effect of elevation on p
           alpha[2] * wind[i,j]       # effect of wind on p
    }
  }
}

