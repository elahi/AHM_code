
model {

  # Priors
  p ~ dunif(0, 1)              # Detection probability on prob. scale
  mean.psi ~ dunif(0, 1)       # Occupancy intercept on prob. scale
  beta0 <- logit(mean.psi)     # Occupancy intercept
  beta1 ~ dunif(-20, 20)       # Occupancy slope on vegHt

  # Likelihood
  for (i in 1:M) {
    # True state model for the partially observed true state
    logit(psi[i]) <- beta0 + beta1 * wind[i]
    z[i] ~ dbern(psi[i])                                          # Occupancy model
    mu.p[i] <- z[i] * p                                           # Detection probability
    y[i] ~ dbin(mu.p[i], n[i])                                    # Detection model
    y.new[i] ~ dbin(z[i] * p, n[i])                               # Simulate new data
    
    # Chi-square discrepancy for a poisson; e is small value to avoid division by zero
    chi2p.data[i] <- pow((y[i] - mu.p[i] * n[i]), 2) / (sqrt(mu.p[i] * n[i]) + e) 
    chi2p.sim[i] <- pow((y.new[i] - mu.p[i] * n[i]), 2) / (sqrt(mu.p[i] * n[i]) + e) 
    
    # Chi-square discrepancy for a binomial; e is small value to avoid division by zero
    chi2b.data[i] <-  pow((y[i] - mu.p[i] * n[i]) / sqrt((mu.p[i] + e) * n[i] * (1 - mu.p[i] - e)), 2)
    chi2b.sim[i] <-  pow((y.new[i] - mu.p[i] * n[i]) / sqrt((mu.p[i] + e) * n[i] * (1 - mu.p[i] - e)), 2)

  }
  
  # Derived quantities
  N.occ <- sum(z[])       # Number of occupied sites among sample of M
  psi.fs <- N.occ/M       # Proportion of occupied sites among sample of M
  
  # Chi-square, poisson [Mackenzie and Bailey, 2004]
  d.chi2p.data <- sum(chi2p.data)
  d.chi2p.sim <- sum(chi2p.sim)
  p.chi2p <- step(d.chi2p.sim - d.chi2p.data)
  
  # Chi-square, binomial [Tobler et al. 2015]
  d.chi2b.data <- sum(chi2b.data)
  d.chi2b.sim <- sum(chi2b.sim)
  p.chi2b <- step(d.chi2b.sim - d.chi2b.data)
  
  mean.data <- mean(y)
  mean.new  <- mean(y.new)
  p.mean <- step(mean.new - mean.data)

}

