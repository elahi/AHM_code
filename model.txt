
model {

  # Priors
  mean.p ~ dunif(0, 1)         # Detection intercept on prob. scale
  alpha0 <- logit(mean.p)      # Detection intercept
  alpha1 ~ dunif(-20, 20)      # Detection slope on wind
  mean.psi ~ dunif(0, 1)       # Occupancy intercept on prob. scale
  beta0 <- logit(mean.psi)     # Occupancy intercept
  beta1 ~ dunif(-20, 20)       # Occupancy slope on vegHt

  # Likelihood
  for (i in 1:M) {
    # True state model for the partially observed true state
    z[i] ~ dbern(psi[i])      # True occupancy z at site i
    logit(psi[i]) <- beta0 + beta1 * vegHt[i]
    for (j in 1:J) {
      # Observation model for the actual observations
      y[i,j] ~ dbern(z[i] * p[i,j])    # Detection-nondetection at i and j
      logit(p[i,j]) <- alpha0 + alpha1 * wind[i,j]
    }
  }

  # Derived quantities
  N.occ <- sum(z[])       # Number of occupied sites among sample of M
  psi.fs <- N.occ/M       # Proportion of occupied sites among sample of M
}

